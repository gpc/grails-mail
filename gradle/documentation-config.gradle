import org.grails.gradle.plugin.doc.PublishGuideTask

apply plugin: 'org.grails.grails-doc'

configurations.register('groovydocConfiguration')
configurations.register('guideConfiguration')

dependencies {

    add('groovydocConfiguration', localGroovy(), {
        because 'groovydoc needs to run with the same version as Gradle'
    })

    add('guideConfiguration', libs.grails.docs)
    add('guideConfiguration', libs.groovy.templates)
    add('guideConfiguration', libs.slf4j.nop) // Get rid of warning about missing slf4j implementation during docs task
}

tasks.withType(Groovydoc).configureEach {
    access = GroovydocAccess.PRIVATE
    processScripts = false
    includeMainForScripts = false
    includeAuthor = true
    classpath = configurations.groovydocConfiguration
    groovyClasspath = configurations.groovydocConfiguration
}

tasks.withType(PublishGuideTask).configureEach {
    classpath = configurations.guideConfiguration
    javadocDir = null
    propertiesFile = layout.projectDirectory.file('src/docs/guide.properties').asFile
}